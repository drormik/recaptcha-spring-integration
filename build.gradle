apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.dtrunk90'
archivesBaseName = 'recaptcha-spring-integration'
version = '1.0.0'

repositories {
	jcenter()
}

dependencies {
	compile 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
	compile 'javax.servlet:javax.servlet-api:3.0.1'
	compile 'javax.validation:validation-api:1.1.0.Final'
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.springframework:spring-webmvc:4.3.8.RELEASE'
	compile 'org.springframework.boot:spring-boot-autoconfigure:1.5.3.RELEASE'
	compile 'org.springframework.security:spring-security-core:4.2.2.RELEASE'

	testCompile 'junit:junit:4.12'
	testCompile 'org.glassfish:javax.el:3.0.0'
	testCompile 'org.hibernate:hibernate-validator:5.4.1.Final'
	testCompile 'org.slf4j:slf4j-simple:1.7.25'
	testCompile 'org.springframework:spring-test:4.3.8.RELEASE'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
			}

			pom.project {
				name 'reCAPTCHA Spring Integration'
				packaging 'jar'
				description 'reCAPTCHA Integration into Spring Framework'
				url 'https://github.com/dtrunk90/recaptcha-spring-integration'

				scm {
					connection 'scm:git:git://github.com/dtrunk90/recaptcha-spring-integration.git'
					developerConnection 'scm:git:git@github.com:dtrunk90/recaptcha-spring-integration.git'
					url 'https://github.com/dtrunk90/recaptcha-spring-integration'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'dtrunk90'
						name 'Danny Trunk'
						email 'dtrunk90@gmail.com'
					}
				}
			}
		}
	}
}
